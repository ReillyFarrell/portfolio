---
layout: article
title: "Etude"
modified:
categories: music
excerpt: "Movement 1 of Sampling Suite for Masashi Hamauzu"
tags: []
image:
  feature:
  teaser:
  thumb:
date: 2015-5-01
---
####(from "Etude No. 8 in G-Minor, Op. 4")
This is the [first movement](https://soundcloud.com/capybarrage-reilly/etude-from-etude-no-8-in-g-minor-op-4?in=capybarrage-reilly/sets/sampling-suite-for-masashi-1) of my [Sampling Suite for Masashi Hamauzu.](https://soundcloud.com/capybarrage-reilly/sets/sampling-suite-for-masashi-1)  The project involved creating new music using five to ten seconds of pre-composed material.

This movement slowly reveals its entire sample as the values for the "legato" parameter increase.  Then the duration of sample (\graindur) is shortened and the frequency of samples triggered (\dur) shrinks with it, creating the effect of a changing meter until all that remains of the sample is a short, percussive chord.

*Sample taken from: "Etude No. 8 in G-Minor, Op. 4" composed by Masashi Hamauzu.  Please support his official works!*


#Code
{% highlight javascript linenos=table %}
(
SynthDef(\granny, {|att = 0.0001, sus = 1, rel = 0.0001, gate = 1, amp = 1, trig = 0, graindur = 0.5, sndbuf, transp = 1, pos = 0, pan = 0, envbuf = -1, mix = 0.33, room = 0.1, damp = 0.1, interp = 2|
	var env, snd;
	env = EnvGen.kr(Env.asr(att, sus, rel), gate, amp, doneAction:2);
	snd = GrainBuf.ar(2, Impulse.kr(trig), graindur, sndbuf, transp, pos, interp, pan, envbuf);
	snd = FreeVerb2.ar(snd, snd, mix, room, damp);
	Out.ar(0, Splay.ar(snd)*env);
}).add;

a = Buffer.readChannel(s, "/Users/reillyfarrell/Desktop/Compositions/Granular Synthesis Suite for Masashi Hamauzu (SuperCollider)/Sample Soundfiles/Etude_Sample.wav", 0, -1, [0]);
b = Buffer.readChannel(s, "/Users/reillyfarrell/Desktop/Compositions/Granular Synthesis Suite for Masashi Hamauzu (SuperCollider)/Sample Soundfiles/Etude_Sample.wav", 0, -1, [1]);

z = Buffer.sendCollection(s, Env.asr(0.01, 1, 1).discretize, 1);
)

/////Final Score/////
(
Pbind(
	\instrument, \granny,
	\sndbuf, [a, b],
	\envbuf, z,
	\mix,0,
	\dur, Pseq([Pn(1.17, 100), Pseries(1.17, -0.0042, 260)]),
	\graindur, Pkey(\dur)*Pseries(4, -0.008, 360),
	\pos, 0,
	\transp, Pseq([Pseries(1.00, 0.0001, 180), Pseries(1.018, -0.0001, 180)]),
	\amp, Pseries(5, 0.025, 360),
	\legato, Pseq([Pseries(0.1, 0.01, 100), Pseries(1, 0.0025, 200), Pn(1.5, 60)]),
).trace.play;
)

s.record;
s.stopRecording;
{% endhighlight %}
