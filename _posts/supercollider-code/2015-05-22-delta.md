---
layout: article
title: "δ"
modified:
categories: supercollider-code
excerpt: "Movement 3 of Sampling Suite for Masashi Hamauzu"
tags: []
image:
  feature:
  teaser: isle.jpg
  thumb:
date: 2015-05-03
---
####δ (from "δ-7" and "δ-17")
This is the [third movement](https://soundcloud.com/capybarrage-reilly/etude-from-etude-no-8-in-g-minor-op-4?in=capybarrage-reilly/sets/sampling-suite-for-masashi-1) of my [Sampling Suite for Masashi Hamauzu.](https://soundcloud.com/capybarrage-reilly/sets/sampling-suite-for-masashi-1)  The project involved creating new music using five to ten seconds of pre-composed material.

This movement varies the rates of its samples to create a dense field of polyrhythms.  

*Samples taken from: "δ-7" and "δ-17" composed by Masashi Hamauzu.  Please support his official works!*


#Code
{% highlight javascript linenos=table %}
// δ

// The Synth Definition //
(
SynthDef("hamitup", {arg trigger = 0, graindur = 0.2, sndbuf, transp = 1, pos = 0, pan = 0, envbuf = -1, amp = 1, gate = 1;
var env, snd;
env = EnvGen.kr(Env.asr, gate, levelScale: amp, doneAction: 2);
snd = GrainBuf.ar(
numChannels: 2,
trigger: Impulse.kr(trigger),
dur: graindur,
sndbuf: sndbuf,
rate: transp,
pos: pos,
pan: pan,
envbufnum: envbuf);
Out.ar(0, snd*env);
}).add;

// The WAV Files //
a = Buffer.readChannel(s, "/Users/reillyfarrell/Desktop/Compositions/Granular Synthesis Suite for Masashi Hamauzu (SuperCollider)/Sample Soundfiles/δ_Sample2.wav", channels: [0]);

b = Buffer.readChannel(s, "/Users/reillyfarrell/Desktop/Compositions/Granular Synthesis Suite for Masashi Hamauzu (SuperCollider)/Sample Soundfiles/δ_Sample1.wav", channels: [0]);

// The New Envelope //
z = Buffer.sendCollection(s, Env.linen(0.001, 10, 0.001).discretize, 1);
)

//"...Phrasing!"

//Verse 1
(~verse1 = Pbind(
\instrument, "hamitup",
\trigger, 0,
\graindur, 0.2,
\sndbuf, b,
\transp, 1,
\pos, Pseq([0.40, 0.67, 0.60], 24),
\pan, Pseq([
Pseq([Pn(0.50, 3), Pn(-0.50, 3)], 8),
Pseq([Pn(0.50, 2), Pn(-0.50, 2)], 6),
], 1),
\amp, Pseq([
Pseries(6, 0.04, 24), Pseries(6.96, -0.04, 24),
Pseries(6, 0.08, 12), Pseries(6.96, -0.08, 12),
]*2, 1),
\envbuf, z,
\dur, 1/6
);

//Verse 2
~verse2 = Pbind(
\instrument, "hamitup",
\trigger, 0,
\graindur, 0.2,
\sndbuf, Pseq([Pn(b, 60), Pn(a, 12)], 1),
\transp, 1,
\pos, Pseq([0.40, 0.67, 0.60], 24),
\pan, Pseq([
Pseq([Pn(0.50, 3), Pn(-0.50, 3)], 8),
Pseq([Pn(0.50, 2), Pn(-0.50, 2)], 6),
], 1),
\amp, Pseq([
Pseries(6, 0.04, 24), Pseries(6.96, -0.04, 24),
Pseries(6, 0.08, 12), Pseries(6.96, -0.08, 12),
]*2, 1),
\envbuf, z,
\dur, 1/6
);

//Verse 3
~verse3 = Pbind(
\instrument, "hamitup",
\trigger, 0,
\graindur, 0.2,
\sndbuf, Pseq([Pn(b, 36), Pn(a, 12)], 1),
\transp, 1,
\pos, Pseq([0.40, 0.67, 0.60], 16),
\pan, Pseq([
Pseq([Pn(0.50, 3), Pn(-0.50, 3)], 4),
Pseq([Pn(0.50, 2), Pn(-0.50, 2)], 6),
], 1),
\amp, Pseq([
Pseries(6, 0.04, 12), Pseries(6.96, -0.04, 12),
Pseries(6, 0.04, 12), Pseries(6.48, 0.04, 12),
]*2, 1),
\envbuf, z,
\dur, 1/6
);

//Verse 4
~verse4 = Pbind(
\instrument, "hamitup",
\trigger, 0,
\graindur, 0.2,
\sndbuf, Pseq([Pn(b, 36), Pn(a, 12)], 1),
\transp, 1,
\pos, Pseq([0.52, 0.67, 0.60], 16),
\pan, Pseq([
Pseq([Pn(0.50, 3), Pn(-0.50, 3)], 4),
Pseq([Pn(0.50, 2), Pn(-0.50, 2)], 6),
], 1),
\amp, Pseq([
Pseries(7, 0.01, 36), Pseries(7.36, -0.03, 12),
]*2, 1),
\envbuf, z,
\dur, 1/6
);

//Verse 5
~verse5 = Pbind(
\instrument, "hamitup",
\trigger, 0,
\graindur, 0.2,
\sndbuf, Pseq([Pn(b, 36), Pn(a, 24)], 1),
\transp, 1,
\pos, Pseq([0.52, 0.67, 0.60], 20),
\pan, Pseq([
Pseq([Pn(0.50, 3), Pn(-0.50, 3)], 4),
Pseq([Pn(0.50, 2), Pn(-0.50, 2)], 9),
], 1),
\amp, Pseq([
Pseries(7, 0.01, 36), Pseries(7.36, -0.015, 24),
]*2, 1),
\envbuf, z,
\dur, 1/6
);

//Verse 6
~verse6 = Pbind(
\instrument, "hamitup",
\trigger, 0,
\graindur, 0.2,
\sndbuf, b,
\transp, 2,
\pos, Pseq([
Pseq([0.40, 0.67, 0.60], 16),
Pseq([0.41, 0.67, 0.60], 4),
Pseq([0.42, 0.67, 0.60], 4),
Pseq([0.43, 0.67, 0.60], 4),
Pseq([0.44, 0.67, 0.60], 4),
Pseq([0.45, 0.67, 0.60], 4),
Pseq([0.46, 0.67, 0.60], 4),
Pseq([0.47, 0.67, 0.60], 4),
Pseq([0.48, 0.67, 0.60], 4),
Pseq([0.49, 0.67, 0.60], 4),
Pseq([0.50, 0.67, 0.60], 4),
Pseq([0.51, 0.67, 0.60], 4),
Pseq([0.52, 0.67, 0.60], 4),
], 1),
\pan, Pseq([
Pseq([Pn(0.50, 3), Pn(-0.50, 3)], 8),
Pseq([Pn(0.50, 3), Pn(-0.50, 3)], 8),
Pseq([Pn(0.50, 3), Pn(-0.50, 3)], 8),
Pseq([Pn(0.50, 3), Pn(-0.50, 3)], 8),
]*2, 1),
\amp, Pseries(4, 0.005, 192),
\envbuf, z,
\dur, 1/6
);

//Verse 7
~verse7 = Pbind(
\instrument, "hamitup",
\trigger, 0,
\graindur, 0.2,
\sndbuf, Pseq([Pn(b, 36), Pn(a, 12)], 2),
\transp, Pseq([
Pn([1, 2], 15), Pn([1, 1.01], 9),
Pn([1, 2], 12), Pn([1, 1.01], 12),
Pn([1, 2], 9), Pn([1, 1.01], 3), Pn([1, 1.02], 3), Pn([1, 1.03], 3), Pn([1, 1.04], 3), Pn([1, 1.05], 3),
Pn([1, 2], 12), Pn([1, 1.99], 12)
], 1),
\pos, Pseq([0.52, 0.67, 0.60], 32),
\pan, Pseq([
Pseq([Pn(0.50, 3), Pn(-0.50, 3)], 4),
Pseq([Pn(0.50, 2), Pn(-0.50, 2)], 6),
], 2),
\amp, Pseq([
Pseries(4, 0.01, 36), Pseries(4.36, -0.03, 12),
]*2, 2),
\envbuf, z,
\dur, 1/6
);

//Verse 8
~verse8 = Pbind(
\instrument, "hamitup",
\trigger, 0,
\graindur, 0.2,
\sndbuf, Pseq([Pn(b, 36), Pn(a, 12)], 2),
\transp, Pseq([
		Pseq([Pn([1, 2], 3), Pn([1, 1, 2], 3), Pn([1, 1, 1, 2], 3), Pn([1, 1, 2], 3)], 3),
		Pn(1, 12),
		Pn([1, 2], 3), Pn([1, 1, 2], 3), Pn([1, 1, 1, 2], 3), Pn([1, 1, 2.01], 3),
		Pn([1, 2.02], 3), Pn([1, 1, 2.03], 3), Pn([1, 1, 1, 2.04], 3), Pn([1.01, 1, 2.05], 3),
		Pn([1.02, 2.04], 3), Pn([1.03, 1, 2.03], 3), Pn([1.02, 1, 1, 2.02], 3), Pn([1.01, 1, 2.01], 3),
		Pn(1, 12),
], 1),
\pos, Pseq([0.52, 0.67, 0.60], 32),
\pan, Pseq([
Pseq([Pn(0.50, 3), Pn(-0.50, 3)], 4),
Pseq([Pn(0.50, 2), Pn(-0.50, 2)], 9),
], 2),
\amp, Pseq([
Pseq([Pn(4, 3), Pn(3, 3), Pn(2, 3), Pn(3, 3)], 3),
Pn(6, 12),
]*2, 2),
\envbuf, z,
\dur, 1/6
);

//Verse 9
~verse9 = Pbind(
\instrument, "hamitup",
\trigger, 0,
\graindur, 0.2,
\sndbuf, Pseq([Pn(b, 36), Pn(a, 36)], 1),
\transp, Pseq([Pn(1, 60), Pn([1, 1.01], 12)], 1),
\pos, Pseq([0.52, 0.67, 0.60], 24),
\pan, Pseq([
Pseq([Pn(0.50, 3), Pn(-0.50, 3)], 4),
Pseq([Pn(0.50, 2), Pn(-0.50, 2)], 12),
], 1),
\amp, Pseq([
Pseq([Pseries(7, 0.1, 6), Pseries(7.6, -0.1, 6)], 3),
Pn(7, 24),
Pn(4, 24),
]*2, 1),
\envbuf, z,
\dur, 1/6
);
)

/////Final Score/////
(
Pseq([
~verse1,
~verse2,
~verse2,
~verse3,
~verse4,
~verse4,
~verse5,
~verse6,
~verse7,
~verse8,
~verse9,
], 1).play;

Pbind(
\instrument, "hamitup",
\trigger, 0,
\graindur, Pseq([0.5, 0.55], inf),
\sndbuf, b,
\transp, 2,
\pos, Pseq([0.40, 0.18], inf),
\pan, 1,
\amp, Pseq([Pn(0, 12), Pseries(0, 0.02, 72), Pn(1.44, 389), Pseries(1.44, -0.04, 36)]*2, 1),
\envbuf, z,
\dur, 1/3,
).play;

Pbind(
\instrument, "hamitup",
\trigger, 0,
\graindur, Pseq([0.5, 0.55], inf),
\sndbuf, b,
\transp, 2,
\pos, Pseq([0.40, 0.18], inf),
\pan, -1,
\amp, Pseq([Pn(0, 8), Pseries(0, 0.03, 48), Pn(1.44, 269), Pseries(1.44, -0.12, 12)]*2, 1),
\envbuf, z,
\dur, 1/2,
).play;
)

s.record;
s.stopRecording;
{% endhighlight %}
